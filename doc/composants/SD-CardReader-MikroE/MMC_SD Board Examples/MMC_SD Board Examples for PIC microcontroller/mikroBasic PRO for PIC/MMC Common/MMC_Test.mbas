' *
' * Project name:
'     Mmc_Test (MMC Library Example)
' * Copyright:
'     (c) MikroElektronika, 2005-2010
' * Revision History:
'     20081016:
'       - initial release;
'     20101102:  
'       - added active comments;
' * Description:
'     MMC library test. Upon flashing, insert a MMC/SD card into the
'     module, when you should receive
'     the "Init-OK" message. Then, you can experiment with MMC read and write
'     functions, and observe the results through the Usart Terminal.
' * Test configuration:
'     MCU:             PIC18F4520
'                      http://ww1.microchip.com/downloads/en/DeviceDoc/39631E.pdf
'     Dev.Board:       EasyPIC6
'                      http://www.mikroe.com/eng/products/view/297/easypic6-development-system/
'     Oscillator:      HS-PLL, 32.00000 MHz
'     Ext. Modules:    ac:MMC-SD_Board on PORTC
'     SW:              mikroBasic PRO for PIC
'                      http://www.mikroe.com/eng/products/view/9/mikrobasic-pro-for-pic/
' * NOTES:
'     - Be sure to connect the RS232 cable and set the jumpers properly!
'     - Be sure to connect te MMC/SD card on PORTC, where the MSSP (SPI) module
'       is located!
'     - The mmc library is available for PIC18 series only!
'     - This is NOT an example for working with FILES on a MMC/SD memory module!
'     - This example should work both for MMC and SD media types!
' *

program MMC_Test

dim MMC_chip_select as sbit at LATC0_bit  ' for writing to output pin always use latch (PIC18 family)
dim MMC_chip_select_direction as sbit at TRISC0_bit

const FILL_CHAR = "m"
dim   i, SectorNo as word
dim   mmc_error as byte
dim   data_ok as bit

' Variables for MMC routines
  SectorData as byte[512]        ' Buffer for MMC sector reading/writing
  data_for_registers as byte[16] ' buffer for CID and CSD registers

' UART write text and new line (carriage return + line feed)
sub procedure UART_Write_Line(dim byref uart_text as string)
  UART1_Write_Text(uart_text)
  UART1_Write(13)
  UART1_Write(10)
end sub

' Display byte in hex
sub procedure printhex(dim i as byte)
dim high, low as byte
  
    high = i and 0xF0                ' High nibble
    high = high >> 4
    high = high + "0"
    if ( high > "9" ) then
      high = high + 7
    end if
    low = (i and 0x0F) + "0"         ' Low nibble
    if ( low > "9" ) then
      low = low + 7
    end if
    UART1_Write(high)
    UART1_Write(low)
end sub

main:
  ADCON1 = ADCON1 or 0x0F          ' Configure AN pins as digital
  CMCON  = CMCON or 7              ' Turn off comparators
  Delay_ms(500)
  
  ' Initialize UART1 module
  UART1_Init(19200)
  Delay_ms(10)

  UART_Write_Line("PIC-Started")   ' PIC present report

  ' Initialize SPI1 module
  SPI1_Init_Advanced(_SPI_MASTER_OSC_DIV64, _SPI_DATA_SAMPLE_MIDDLE, _SPI_CLK_IDLE_LOW, _SPI_LOW_2_HIGH)

  ' initialise a MMC card
    mmc_error = Mmc_Init()
    if ( mmc_error = 0 ) then
      UART_Write_Line("MMC Init-OK")       ' If MMC present report
      else
        UART_Write_Line("MMC Init-error")  ' If error report
    end if
    ' Fill MMC buffer with same characters
    for i = 0 to 511
      SectorData[i] = FILL_CHAR
    next i

    ' Choose sector
    SectorNo = 590

    ' Write sector
    mmc_error = Mmc_Write_Sector(SectorNo, SectorData)
    if ( mmc_error = 0 ) then
      UART_Write_Line("Write-OK")
      else  ' if there are errors.....
        UART_Write_Line("Write-Error")
    end if

    ' Reading of CID register
    mmc_error = Mmc_Read_Cid(data_for_registers)
    if ( mmc_error = 0 ) then
      UART1_Write_Text("CID : ")
      for i = 0 to 15
        printhex(data_for_registers[i])
      next i
      UART_Write_Line("  ")
    else
      UART_Write_Line("CID-error")
    end if

    ' Reading of CSD register
    mmc_error = Mmc_Read_Csd(data_for_registers)
    if ( mmc_error = 0 ) then
      UART1_Write_Text("CSD : ")
      for i = 0 to 15
        printhex(data_for_registers[i])
      next i
        UART_Write_Line("  ")
      else
        UART_Write_Line("CSD-error")
    end if

    ' Read sector
    mmc_error = Mmc_Read_Sector(SectorNo, SectorData)
    if ( mmc_error = 0 ) then
      UART_Write_Line("Read-OK")
      ' Chech data match
      data_ok = 1
      for i = 0 to 511
        UART1_Write(SectorData[i])
        if (SectorData[i] <> FILL_CHAR) then
          data_ok = 0
          break
        end if
      next i
      
      if ( data_ok <> 0 ) then
        UART_Write_Line("Content-OK")
      else
        UART_Write_Line("Content-Error")
      end if
    else  ' if there are errors.....
      UART_Write_Line("Read-Error")
    end if

    ' Signal test end
    UART_Write_Line("Test End.")
end.